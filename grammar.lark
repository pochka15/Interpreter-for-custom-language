start: (function_declaration (_NL function_declaration)*)?

function_declaration: NAME "(" function_parameters ")" FUNCTION_RETURN_TYPE? "{" statements_block "}"

function_parameters: (function_parameter ("," function_parameter)*)?

function_parameter: NAME type

type: "(" type ")" | simple_type ("." simple_type)*

// Hide
simple_type: NAME

// Hide
statement: assignment | for_statement | while_statement | expression | jump_statement

variable_declaration: (VAR | CONST) NAME type

statements_block: (statement (_NL statement+)*)?

assignment: directly_assignable_expression ASSIGNMENT_OPERATOR expression
  | assignable_expression ASSIGNMENT_AND_OPERATOR expression

// Hide
assignable_expression: prefix_unary_expression 
  | "(" prefix_unary_expression ")"

// Hide
directly_assignable_expression : postfix_unary_expression assignable_suffix?
  | variable_declaration
  | "(" directly_assignable_expression ")"

// Hide
assignable_suffix: indexing_suffix
  | navigation_suffix

for_statement: "for" NAME "in" expression "{" statements_block "}"

while_statement: "while" expression "{" statements_block "}"

// Hide
expression: disjunction

// Hide
disjunction: conjunction ("||" conjunction)*

// Hide
conjunction: equality ("&&" equality)*

// Hide
equality: comparison (EQUALITY_OPERATOR comparison)?

// Hide
comparison: additive_expression (COMPARISON_OPERATOR additive_expression)*

function_call_arguments: expression ("," expression)*

// Hide
additive_expression: multiplicative_expression (ADDITIVE_OPERATOR multiplicative_expression)*

// Hide
multiplicative_expression: prefix_unary_expression (MULTIPLICATIVE_OPERATOR prefix_unary_expression)*

// Hide
prefix_unary_expression: PREFIX_OPERATOR* postfix_unary_expression

// Hide
postfix_unary_expression: primary_expression postfix_unary_suffix*

// Hide
postfix_unary_suffix: call_suffix | indexing_suffix | navigation_suffix

call_suffix: "(" function_call_arguments? ")"

indexing_suffix: "[" expression "]"

navigation_suffix: "." NAME

// Hide
primary_expression: "(" expression ")"
  | identifier
  | simple_literal
  | collection_literal
  | if_expression

// Hide
identifier: NAME

collection_literal: "[" expression ("," expression)* "]" | "[" "]"

jump_statement: RETURN expression?
  | BREAK

if_expression: "if" expression "{" statements_block "}"
  | "if" expression "{" statements_block "}" ELSE "{" statements_block "}"
  | "if" expression "{" statements_block "}" ELSE if_expression

// Hide
simple_literal: STRING | BOOLEAN | DEC_NUMBER | FLOAT_NUMBER

%import python.DEC_NUMBER
%import python.COMMENT
%import python.STRING
%import common.LETTER
%import common.DIGIT
%import common.WS
%import common.NEWLINE -> _NL

%ignore WS
%ignore COMMENT

// Terminals
EQUALITY_OPERATOR: "!=" | "=="
ASSIGNMENT_OPERATOR: "="
BOOLEAN: "true" | "false"
FLOAT_NUMBER: /((\d+\.[\d_]*|\.[\d_]+)(e[-+]?\d+)?|\d+(e[-+]?\d+))/i
FUNCTION_RETURN_TYPE: NAME
COMPARISON_OPERATOR:  "<" | ">" | "<=" | ">="
ADDITIVE_OPERATOR: "+" | "-"
PREFIX_OPERATOR: ADDITIVE_OPERATOR | "!"
MULTIPLICATIVE_OPERATOR: "*" | "/" | "%"
ASSIGNMENT_AND_OPERATOR: "+=" | "-=" | "*=" | "/=" | "%="
RELATIVE_LOCATION: /\.+/
RETURN: "ret"
BREAK: "break"
ELSE: "else"
CONST: "c"
VAR: "v"
NAME: /(?!(for|while|true|false|ret|break|else|in|v|c)\b)[a-zA-Z_][a-zA-Z_0-9]*/